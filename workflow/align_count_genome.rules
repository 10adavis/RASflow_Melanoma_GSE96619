import pandas as pd
configfile: "configs/config_align_count_genome.yaml"

samples = pd.read_csv(config["METAFILE"], sep = '\t', header = 0)['sample']
indexes = list(range(1, 9))
trimmed = config["TRIMMED"]
if trimmed == "no":
    input_path = config["INPUTPATH"]
else:
    input_path = config["INPUTPATH_TRIM"]
end = config["END"]
intermediate_path = config["OUTPUTPATH"] + "/" + config["PROJECT"] + "/genome"
final_path = config["FINALOUTPUT"] + "/" + config["PROJECT"] + "/genome"

rule end:
    input:
        report = final_path + "/report_align_count.html"

if end == "pair":
    rule getReads:
        input:
            input_path = directory(input_path)
        output:
            forward = temp(intermediate_path + "/reads/{sample}_forward.fastq.gz"),
            reverse = temp(intermediate_path + "/reads/{sample}_reverse.fastq.gz")
        run:
            shell("cp {input.input_path}/{wildcards.sample}_*R1*.f*q.gz {output.forward}"),
            shell("cp {input.input_path}/{wildcards.sample}_*R2*.f*q.gz {output.reverse}")
else:
    rule getReads:
        input:
            input_path = directory(input_path)
        output:
            forward = temp(intermediate_path + "/reads/{sample}.fastq.gz")
        run:
            shell("cp {input.input_path}/{wildcards.sample}*.f*q.gz {output.forward}")

rule indexGenome:
    input:
        genome = config["GENOME"]
    output:
        indexes = expand(intermediate_path + "/indexes/index.{index}.ht2", index = indexes),
        splicesites = intermediate_path + "/splicesites.txt"
    params:
        index = intermediate_path + "/indexes/index"
    shell:
        "hisat2-build -p {config[NCORE]} {input.genome} {params.index}"
        "&& hisat2_extract_splice_sites.py {config[ANNOTATION]} > {output.splicesites}"

if end == "pair":
    rule alignment:
        input:
            index = expand(intermediate_path + "/indexes/index.{index}.ht2", index = indexes),
            splicesites = intermediate_path + "/splicesites.txt",
            forward = intermediate_path + "/reads/{sample}_forward.fastq.gz",
            reverse = intermediate_path + "/reads/{sample}_reverse.fastq.gz"
        output:
            sam = temp(intermediate_path + "/samFile/{sample}.sam"),
            bam = temp(intermediate_path + "/bamFile/{sample}.bam")
        params:
            index = intermediate_path + "/indexes/index"
        benchmark:
            intermediate_path + "/benchmarks/{sample}.hisat2.benchmark.txt"
        run:
            shell("hisat2 -p {config[NCORE]} --known-splicesite-infile {input.splicesites} -x {params.index} -1 {input.forward} -2 {input.reverse} -S {output.sam}")
            shell("samtools view -@ {config[NCORE]} -b -S {output.sam} > {output.bam}")
else:
    rule alignment:
        input:
            index = expand(intermediate_path + "/indexes/index.{index}.ht2", index = indexes),
            splicesites = intermediate_path + "/splicesites.txt",
            forward = intermediate_path + "/reads/{sample}.fastq.gz"
        output:
            sam = temp(intermediate_path + "/samFile/{sample}.sam"),
            bam = temp(intermediate_path + "/bamFile/{sample}.bam")
        params:
            index = intermediate_path + "/indexes/index"
        benchmark:
            intermediate_path + "/benchmarks/{sample}.hisat2.benchmark.txt"
        run:
            shell("hisat2 -p {config[NCORE]} --known-splicesite-infile {input.splicesites} -x {params.index} -U {input.forward} -S {output.sam}")
            shell("samtools view -@ {config[NCORE]} -b -S {output.sam} > {output.bam}")

rule sortBAM:
    input:
        bam = intermediate_path + "/bamFile/{sample}.bam"
    output:
        sort = intermediate_path + "/bamFileSort/{sample}.sort.bam"
    shell:
        "samtools sort -@ {config[NCORE]} {input.bam} -o {output.sort}"

rule alignmentQC:
    input:
        sort = intermediate_path + "/bamFileSort/{sample}.sort.bam"
    output:
        bamqc = directory(final_path + "/alignmentQC/{sample}_BAMqc")
    shell:
        "qualimap bamqc -bam {input.sort} -outdir {output.bamqc}"

rule featureCount:
    input:
        sort = intermediate_path + "/bamFileSort/{sample}.sort.bam",
        annotation = config["ANNOTATION"]
    output:
        count = final_path + "/countFile/{sample}_count.tsv",
        count_summary = final_path + "/countFile/{sample}_count.tsv.summary"
    run:
        if config["COUNTER"]=="featureCounts":
            if config["END"]=="pair":
                shell("featureCounts -p -T {config[NCORE]} -t exon -g {config[ATTRIBUTE]} -a {input.annotation} -o {output.count} {input.sort} && tail -n +3 {output.count} | cut -f1,7 > temp.{wildcards.sample} && mv temp.{wildcards.sample} {output.count}")
            else:
                shell("featureCounts -T {config[NCORE]} -t exon -g {config[ATTRIBUTE]} -a {input.annotation} -o {output.count} {input.sort} && tail -n +3 {output.count} | cut -f1,7 > temp.{wildcards.sample} && mv temp.{wildcards.sample} {output.count}")
        elif config["COUNTER"]=="htseq-count":
            shell("htseq-count -f bam -i {config[ATTRIBUTE]} -s no -t exon {input.sort} {input.annotation} | sed '/^__/ d' > {output.count}")

rule summaryReport:
    input:
        bamqc = expand(final_path + "/alignmentQC/{sample}_BAMqc", sample = samples),
        count_summary = expand(final_path + "/countFile/{sample}_count.tsv.summary", sample = samples)
    output:
        report = final_path + "/report_align_count.html"
    shell:
        "multiqc {input.bamqc} {input.count_summary} --filename {output.report}"
